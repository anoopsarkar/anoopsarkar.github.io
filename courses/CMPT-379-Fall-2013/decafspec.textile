<notextile>
<html>
<head>
<title>Decaf Language Reference. cmpt-379 course on Compilers: Burnaby Campus</title>
<meta name="viewport" content="width=device-width, initial-scale=1.0">
<!-- Bootstrap -->
<link href="css/bootstrap.min.css" rel="stylesheet" media="screen">
<!-- <link href="css/bootstrap-theme.min.css" rel="stylesheet" media="screen"> -->
<style>
body {
    padding-top: 5px;
}
</style>
<link rel="shortcut icon" href="favicon32.ico" type="image/x-icon" />

<!-- HTML5 shim and Respond.js IE8 support of HTML5 elements and media queries -->
<!--[if lt IE 9]>
  <script src="../../assets/js/html5shiv.js"></script>
  <script src="../../assets/js/respond.min.js"></script>
<![endif]-->
</head>
<body>
<div class="container">
</notextile>

h1. The Decaf Programming Language Specification <small>(for CMPT 379 Fall 2013)</small>

h2. Introduction

This is the reference manual for the Decaf programming language which is the programming language specifically for the "CMPT 379: Compilers":http://www.cs.sfu.ca/~anoop/teaching/CMPT-379-Fall-2013/index.html course taught by "Anoop Sarkar":http://www.cs.sfu.ca/~anoop/ at the SFU Computing Science School (Burnaby campus). For more information and other documentation, see the course web page.

Decaf is a strongly typed C-like language. The feature set is trimmed down considerably from what is usually part of a full-fledged programming language. This is done to keep the programming assignments manageable. Despite these limitations, Decaf will be able to handle interesting and non-trivial programs.

Here is an example Decaf program:

bc.. extern void print_int(int);

class GreatestCommonDivisor {
    int a = 10;
    int b = 20;
    
    int main() {
       int x, y, z;
       x = a;
       y = b;
       z = gcd(x, y);

       // print_int is part of the standard input-output library
       print_int(z); 
    }

    // function that computes the greatest common divisor
    int gcd(int a, int b) {
       if (b == 0) { return(a); } 
       else { return( gcd(b, a % b) ); }
    }
}

h2. Notation

The syntax is specified using "Extended Backus-Naur Form":http://en.wikipedia.org/wiki/Extended_Backus-Naur_Form (EBNF):

bc. Production  = production_name "=" [ Expression ] "." .
Expression  = Alternative { "|" Alternative } .
Alternative = Term { Term } .
Term        = production_name | token [ "..." token ] | Group | Option | Repetition .
Group       = "(" Expression ")" .
Option      = "[" Expression "]" .
Repetition  = "{" Expression "}" .
CommaList   = "{" Expression "}+" .

Productions are expressions constructed from terms and the following operators, in increasing precedence:

bc. |   alternation
()  grouping
[]  option (0 or 1 Expression)
{}  repetition (0 to n Expressions)
{}+ comma list (1 to n Expressions comma separated, e.g. x, y, z)

Lower-case production names are used to identify lexical tokens. Non-terminals are in CamelCase. Lexical tokens are enclosed in double quotes "" or back quotes ``.

The form @a ... b@ represents the set of characters from a through b as alternatives. The horizontal ellipsis @...@ is also used elsewhere in the spec to informally denote various enumerations or code snippets that are not further specified. The character @...@ is not a token of the Decaf language.

h2. Source code representation

Decaf source code is encoded as ASCII text. Upper and lower case characters are considered different characters. For example, @if@ is defined as a keyword, but @IF@ would be considered an identifier.

h3. ASCII table

The ASCII table and decimal equivalent for each character is shown below:

bc.   0 nul    1 soh    2 stx    3 etx    4 eot    5 enq    6 ack    7 bel
  8 bs     9 ht    10 nl    11 vt    12 np    13 cr    14 so    15 si
 16 dle   17 dc1   18 dc2   19 dc3   20 dc4   21 nak   22 syn   23 etb
 24 can   25 em    26 sub   27 esc   28 fs    29 gs    30 rs    31 us
 32 sp    33  !    34  "    35  #    36  $    37  %    38  &    39  '
 40  (    41  )    42  *    43  +    44  ,    45  -    46  .    47  /
 48  0    49  1    50  2    51  3    52  4    53  5    54  6    55  7
 56  8    57  9    58  :    59  ;    60  <    61  =    62  >    63  ?
 64  @    65  A    66  B    67  C    68  D    69  E    70  F    71  G
 72  H    73  I    74  J    75  K    76  L    77  M    78  N    79  O
 80  P    81  Q    82  R    83  S    84  T    85  U    86  V    87  W
 88  X    89  Y    90  Z    91  [    92  \    93  ]    94  ^    95  _
 96  `    97  a    98  b    99  c   100  d   101  e   102  f   103  g
104  h   105  i   106  j   107  k   108  l   109  m   110  n   111  o
112  p   113  q   114  r   115  s   116  t   117  u   118  v   119  w
120  x   121  y   122  z   123  {   124  |   125  }   126  ~   127 del

The set of valid characters in Decaf is all the ASCII characters:

bc. all_char = /* all ASCII characters from 7 ... 13 and 32 ... 126 */ .
char = /* all ASCII characters from 7 ... 13 and 32 ... 126 except char 92 "\" */ .

Implementation restriction: For compatibility with other tools, a compiler should always disallow the @nul@ character (decimal: 0) in the source text.

h3. Letters and Digits

The underscore character @_@ is considered a letter.

bc. letter        = "A" ... "Z" | "a" ... "z" | "_" .
decimal_digit = "0" ... "9" .
hex_digit     = "0" ... "9" | "A" ... "F" | "a" ... "f" .
digit         = "0" ... "9" .

h2. Lexical Elements

h3. Comments

Decaf only has line comments that start with the character sequence @//@ and stop at the newline character. The newline character is assumed to be part of the comment. The comment representation is as follows:

bc. // this is a line comment and it includes the newline at the end of the line\n

bc. comment = // { char } \n

h3. Whitespace

Whitespace is used to separate tokens, and is defined as follows:

bc. newline         = /* ASCII character nl : '\n' */ .
carriage_return = /* ASCII character cr : '\r' */ .
horizontal_tab  = /* ASCII character ht : '\t' */ .
vertical_tab    = /* ASCII character vt : '\v' */ .
form_feed       = /* ASCII character np : '\f' */ .
space           = /* ASCII character sp : ' ' */ .
whitespace      = { newline | carriage_return | horizontal_tab | vertical_tab | form_feed | space } .

The following are special characters that are not part of white space:

bc. bell         = /* ASCII character bel : '\a' */ .
backspace    = /* ASCII character bs : '\b' */ .

h3. Tokens

p. Tokens are the vocabulary of the Decaf language. There are four classes: identifiers, keywords, operators and literals.  White space is ignored except as it separates tokens that would otherwise combine into a single token. For example, @int3@ is a single token but @int 3@ is two tokens, a keyword @int@ and integer @3@; and @int(3)@ is a sequence of four tokens: @int@, @(@, @3@ and @)@. 

While breaking the input into tokens, the next token is the longest sequence of characters that form a valid token. 

h3. Semicolons

The Decaf language uses semicolons @;@ as a terminator in a number of productions.

h3. Identifiers

Identifiers name program entities such as variables and types. An identifier is a sequence of one or more letters and digits. The first character in an identifier must be a letter.

bc. identifier = letter { letter | digit } .

For example:

bc. a
x9
_x9
ThisVariableIsInCamelCase

Type and constant identifiers are predeclared.

h3. Keywords

The following keywords are reserved and may not be used as identifiers.

bc. bool    break   continue  class  else   extends 
extern  false   for       if     int    new 
null    return  string    true   void   while  

h3. Operators and Delimiters

The following character sequences represent operators (see Operators section below) and delimiters.

bc. {  }   [   ]   ,   ;   (   )  =  
-  !   +   *   /   <<  <<  <  >  
%  <=  >=  ==  !=  &&  ||  .

h3. Integer literals

An integer literal is a sequence of digits representing an integer constant. An optional prefix sets a non-decimal base: 0x or 0X for hexadecimal. In hexadecimal literals, letters a-f and A-F represent values 10 through 15.

bc. int_lit     = decimal_lit | hex_lit .
decimal_lit = { decimal_digit } .
hex_lit     = "0" ( "x" | "X" ) hex_digit { hex_digit } .

For example, the following are integer literals:

bc. 42
0xBadFace
170141183460469231731687303715884105727

For integer literals, the semantics of range checking occurs later, so that a long sequence of digits such as the last example above which is clearly out of range is still scanned as a single token. The semantic analyzer will come in later and reject this lexeme value as a valid integer constant.

h3. Character literals

A character literal represents a character constant (see Constants section), which is an integer value that identifies an ASCII eqivalent. A character literal is expressed as one or more characters enclosed in single quotes. Within the quotes, any character may appear except single quote and newline. A single quoted character represents the ASCII value of the character itself, while multi-character sequences beginning with a backslash encode special ASCII values as escaped characters.

The simplest form represents the single character within the quotes which is equal to the integer ASCII value. For example:

bc. 'a' // equal to ASCII value 97 stored as an integer type

After a backslash, certain single-character escapes represent special values:

bc. \a   ASCII 7:   alert or bell
\b   ASCII 8:   backspace
\t   ASCII 9:   horizontal tab
\n   ASCII 10:  line feed or newline
\v   ASCII 11:  vertical tab
\f   ASCII 12:  form feed
\r   ASCII 13:  carriage return
\\   ASCII 92:  backslash
\'   single quote
\"   double quote

The following is a list of escaped character codes that refer to the equivalent ASCII codes above.

bc. char_lit     = "'" ( char | escaped_char ) "'" .
escaped_char = "\" ( "n" | "r" | "t" | "v" | "f" | "a" | "b" | `\` | "'" | `"` ) .

The following are some legal and illegal examples of character literals:

bc. 'a'
'\t'
'"'
'\"'
'aa'         // illegal: too many characters
''           // illegal: no characters
'\'          // illegal: invalid closing delimiter

Unterminated character literals must be reported as errors.

h3. String literals

A string literal represents a string constant obtained from concatenating a sequence of characters (also see the Constants section below). 

bc. string_lit = `"` { char | escaped_char } `"` .

A string literal must start and end on a single line, it cannot be split over multiple lines. It can include escape sequences like \n and this is distinct from a newline character inside the string constant. 

For example, the following is legal:

bc. "\n" 

But the following is not legal:

bc. "
"

Escaped characters have to used correctly. The following string has an invalid escaped character: 

bc. "\"

You can have an single quote or escaped single quote in a string. So both of the following are valid.

bc. "'"
"\'"

Unterminated string literals must be reported as errors.

h3. Type literals

The following are the keywords used to specify Decaf types.

bc. int bool void string

h3. Boolean constant literals

The following keywords are used as constants for boolean types.

bc. true false

h3. List of Tokens

The following is an alphabetically sorted list of tokens for Decaf with the token names used in the homework.

bc. T_AND            &&
T_ASSIGN         =
T_BOOLTYPE       bool
T_BREAK          break
T_CHARCONSTANT   char_lit (see section on Character literals)
T_CLASS          class
T_COMMA          ,
T_COMMENT        comment
T_CONTINUE       continue
T_DIV            /
T_DOT            .
T_ELSE           else
T_EQ             ==
T_EXTENDS        extends
T_EXTERN         extern
T_FALSE          false
T_FOR            for
T_GEQ            >=
T_GT             >
T_ID             identifier (see section on Identifiers)
T_IF             if
T_INTCONSTANT    int_lit (see section on Integer literals)
T_INTTYPE        int
T_LCB            {
T_LEFTSHIFT      <<
T_LEQ            <=
T_LPAREN         (
T_LSB            [
T_LT             <
T_MINUS          -
T_MOD            %
T_MULT           *
T_NEQ            !=
T_NEW            new
T_NOT            !
T_NULL           null
T_OR             ||
T_PLUS           +
T_RCB            }
T_RETURN         return
T_RIGHTSHIFT     >>
T_RPAREN         )
T_RSB            ]
T_SEMICOLON      ;
T_STRINGCONSTANT string_lit (see section on String literals)
T_STRINGTYPE     string
T_TRUE           true
T_VOID           void
T_WHILE          while
T_WHITESPACE     whitespace (see section on Whitespace)

h2. Types

Decaf has four types: void, booleans, integers and strings. String types, however, can only be used with extern functions. Void types are for return types of functions only (called MethodType below) and not used in variable declarations.

bc. ExternType = ( string | MethodType ) .
MethodType = ( void | Type ) .
Type = ( int | bool ) .

Decaf also has a limited _array type_ for arrays of integers and booleans.

h3. Boolean types

A boolean type represents the set of Boolean truth values denoted by the predeclared constants @true@ and @false@. The predeclared boolean type is @bool@. This is represented as the LLVM type @Int1@.

bc. BoolConstant = ( true | false ) .

h3. Integer types

A integer type refers to the set of all signed 32 bit integers (-2147483648 to 2147483647) corresponding to the LLVM type @Int32@. The predeclared integer type is @int@.

h3. String types

A string type represents the set of string values. A string value is a (possibly empty) sequence of bytes. Strings are immutable: once created, it is impossible to change the contents of a string. The LLVM type that corresponds to a Decaf string type is @Int8Ptr@. The predeclared string type is @string@.

h3. Array types

Decaf has integer and boolean arrays. However, arrays are declared only in the global (class declaration) scope as part of the field declarations (see FieldDecl). All arrays are one-dimensional and have a size that is fixed at compile-time. Arrays are indexed from 0 to n âˆ’ 1, where n > 0 is the size of the array. The usual bracket notation is used to index arrays. Since arrays have a compile-time fixed size and cannot be declared as method parameters (or local variables), there is no facility to query the length of an array variable in Decaf. Arrays must be initialized to all zeroes at declaration time.

bc. ArrayDecl = identifier "[" int_lit "]" .

h2. Constants

Decaf has _boolean constants_, _integer constants_, and _string constants_. Integer constants can be created using character literals and integer literals. @BoolConstant@ is defined (in the Types section) as either @true@ or @false@.

bc. Constant = ( int_lit | char_lit | BoolConstant ) .

String constants can only be used with extern functions. See the Types section for more details.

h2. Decaf program structure

h3. Program

A Decaf program starts with optional external function declarations followed by the class definition (a Decaf class is more like a module or namespace). A class has optional global variables (called field variables) followed by method (function) definitions.

bc. Program = Externs class identifier "{" FieldDecls MethodDecls "}" .

h3. External Functions

A Decaf program can access external function that are linked, such as the Decaf standard library functions which are implemented in C, and accessed from within the Decaf program as external functions.

bc. Externs    = { ExternDefn } .
ExternDefn = extern MethodType identifier "(" [ { ExternType }+ ] ")" ";" .
 
h3. Global variables 

Decaf has global variables with scope limited to their class that appear before any method declarations. Global variables in Decaf are called _field declarations_. They can be simple declarations without initialization (assumed to be zero initialized by the compiler) or non-array variables can be declared with an assignment to a constant (see Constants section).

bc. FieldDecls = { FieldDecl } .
FieldDecl  = Type { identifier | ArrayDecl }+ ";" 
FieldDecl  = Type identifier "=" Constant ";" .

The assignment to an identifier has to be a constant:

bc. class foo { int a; int b = a; } // Invalid!

h3. Method declarations

Functions or methods in Decaf start with the return type of the method, the name of the method and in parentheses is the argument list.

bc. MethodDecls = { MethodDecl } .
MethodDecl  = MethodType identifier "(" [ { Type identifier }+ ] ")" Block

The program must contain a declaration for a method called @main@ that has no parameters. The return type of the method @main@ has to be type @int@, however the compiler does not enforce a return statement within the @main@ definition (just like ANSI C). Execution of a Decaf program starts at this method @main@. Methods defined as part of the class declaration can have zero or more parameters and must have a return type of type @MethodType@ explicitly defined.

h3. Blocks

Decaf blocks have a section for local variable definitions first followed by statements.

bc. Block = "{" VarDecls Statements "}" .

h3. Variable Declarations

Local variables are declared using a comma separated list for each type. They cannot be assigned a value when they are defined.

bc. VarDecls = { VarDecl } .
VarDecl  = Type { identifier }+ ";" .

There is no assignment allowed for local variables:

bc. int foo() { int a = 10; } // Invalid!

h3. Statements

Statements in Decaf consist of variable assignment, method calls, syntax for various kinds of control flow, special statements for breaking out of or continuing to the top of the block. Statements can also be Blocks (see section on Blocks).

bc. Statement = Block .

h4. Assign statement

bc. Statement = Assign ";" .
Assign    = Lvalue "=" Expr .
Lvalue    = identifier | identifier "[" Expr "]" .

h4. Method calls

bc. Statement  = MethodCall ";" .
MethodCall = identifier "(" [ { MethodArg }+ ] ")" .
MethodArg  = Expr | string_lit .

p. External functions are declared using the extern keyword. These functions are provided at using a separate library which is linked with your Decaf program at runtime. Some minimal type checking is done using the declaration. The most useful library functions that you will use are the @print_string@, @print_int@ and @read_int@ functions.

The return value can be a type that can be assigned to an @Lvalue@:

bc. z = read int(); 

p. In this case, the integer variable @z@ receives the result of calling the @read_int@ library function. The return value can also be declared to be @void@ in which case assigning the output of a library function to an @Lvalue@ will result in a semantic error.

h4. If statement

bc. Statement = if "(" Expr ")" Block [ else Block ] .

h4. While statement

bc. Statement =  while "(" Expr ")" Block .

h4. For statement

The @for@ loop in Decaf has the usual structure @for ( init ; check ; post )@ followed by the @Block@ of the for loop.

bc. Statement = for "(" { Assign }+ ";" Expr ";" { Assign }+ ")" Block .

The init, check and post parts of the @for@ loop cannot be empty:

bc. for(; a < b; ) // Invalid!

h4. Return statement

bc. Statement = return [ "(" [ Expr ] ")" ] ";" .

The following are all valid return statements:

bc. return(3);
return(bool);
return();
return;

h4. Break statement

A @break@ statement terminates execution of the innermost @for@ or @while@ loop (branches to end of loop).

bc. Statement = break ";" .

h4. Continue statement

A @continue@ statement begins the next iteration of the innermost @for@ or @while@ loop at its post statement (see For statement).

bc. Statement = continue ";" .

h2. Expressions

h3. Operands

Operands are the elementary values in an expression. 

bc. Expr = identifier
Expr = MethodCall 
Expr = Constant .

h3. Unary Operators

There are only two unary operators in Decaf. One for logical negation and the other for unary minus of arithmetic expressions. The result of @UnaryNot@ is of type @bool@ and the result of @UnaryMinus@ is of type @int@.

bc. UnaryOperator = ( UnaryNot | UnaryMinus ) .
UnaryNot = "!" .
UnaryMinus = "-" .

h3. Binary Operators

Binary operators are split into boolean binary operators and arithmetic binary operators. The result of using a boolean operator is the type @bool@ and the result of using an arithmetic operator is the type @int@.

bc. BinaryOperator = ( ArithmeticOperator | BooleanOperator ) .
ArithmeticOperator = ( "+" | "-" | "*" | "/" | "<<" | ">>" | "%" ) .
BooleanOperator = ( "==" | "!=" | "<" | "<=" | ">" | ">=" | "&&" | "||" ) .

The boolean connectives @&&@ and @||@ are interpreted using <a href="http://en.wikipedia.org/wiki/Short-circuit_evaluation">short circuit evaluation</a>. This means: the second operand is not evaluated if the result of the first operand determines the value of the whole expression. For example, if the result is @false@ for @&&@ or @true@ for @||@.

Binary @%@ computes the modulus of two numbers. Given two operands of type @int@, @a@ and @b@: If @b@ is positive, then @a % b@ is a minus the largest multiple of @b@ that is not greater than @a@. If @b@ is negative, then @a % b@ is @a@ minus the smallest multiple of @b@ that is not less than @a@ (in this case the result will be less than or equal to zero).

h3. Operators and Precedence

Unary operators have the highest precedence. For the other binary operators the precedence is defined as follows. All operators at the same precedence level get equal precedence. All operators with equal precedence associate left. The @UnaryMinus@ operator associates to the right.

table(table).
|_. Precedence |_. Operator |
| 7 | @UnaryMinus@ |
| 6 | @UnaryNot@ |
| 5 | @* / % << >>@ |
| 4 | @+ -@ |
| 3 | @== != < <= > >=@ |
| 2 | @&&@ |
| 1 | "&#124;&#124;" |

h3. Primary expressions

Primary expressions build larger expressions from operands, operators and parentheses. The parentheses are used to group expressions to obtain different orders of evaluation. Parentheses can be omitted if the desired evaluation is consistent with the precedence rules (see the Operators and Precedence section). The type of the @Expr@ on the left hand side is determined by the @Expr@ on the right hand side and the operator used.

bc. Expr = Expr BinaryOperator Expr
Expr = UnaryOperator Expr
Expr = "(" Expr ")" .

h3. Index expression

In this expression, the @identifier@ must be an Array Type (see section on Array Types). The @Expr@ is evaluated to give an array index and the result of the evaluation must be of type @int@. The integer value is then used to find the element of the array which is of type @int@ or @bool@ depending on the Array Type.

bc. Expr = identifier "[" Expr "]" .


h3. Decaf grammar

bc. Program = Externs class identifier "{" FieldDecls MethodDecls "}" .
Externs    = { ExternDefn } .
ExternDefn = extern MethodType identifier "(" [ { ExternType }+ ] ")" ";" .
FieldDecls = { FieldDecl } .
FieldDecl  = Type { identifier | ArrayDecl }+ ";" 
FieldDecl  = Type identifier "=" Constant ";" .
ArrayDecl = identifier "[" int_lit "]" .
MethodDecls = { MethodDecl } .
MethodDecl  = MethodType identifier "(" [ { Type identifier }+ ] ")" Block
Block = "{" VarDecls Statements "}" .
VarDecls = { VarDecl } .
VarDecl  = Type { identifier }+ ";" .
Statement = Block .
Statement = Assign ";" .
Assign    = Lvalue "=" Expr .
Lvalue    = identifier | identifier "[" Expr "]" .
Statement  = MethodCall ";" .
MethodCall = identifier "(" [ { MethodArg }+ ] ")" .
MethodArg  = Expr | string_lit .
Statement = if "(" Expr ")" Block [ else Block ] .
Statement =  while "(" Expr ")" Block .
Statement = for "(" { Assign }+ ";" Expr ";" { Assign }+ ")" Block .
Statement = return [ "(" [ Expr ] ")" ] ";" .
Statement = break ";" .
Statement = continue ";" .
Expr = identifier
Expr = MethodCall 
Expr = Constant .
UnaryOperator = ( UnaryNot | UnaryMinus ) .
UnaryNot = "!" .
UnaryMinus = "-" .
BinaryOperator = ( ArithmeticOperator | BooleanOperator ) .
ArithmeticOperator = ( "+" | "-" | "*" | "/" | "<<" | ">>" | "%" ) .
BooleanOperator = ( "==" | "!=" | "<" | "<=" | ">" | ">=" | "&&" | "||" ) .
Expr = Expr BinaryOperator Expr
Expr = UnaryOperator Expr
Expr = "(" Expr ")" .
Expr = identifier "[" Expr "]" .

h2. Decaf Semantics

h3. Type Checking

Make sure the following type checks are implemented in the compiler.

* Binary @+@ @-@ @*@ @/@ @%@ @>>@ @<<@ @<@ @>@ @<=@ @>=@ and unary @-@ only work on integer expressions.
* Binary @&&@ @||@ and unary @!@ only work on boolean expressions.
* Binary @==@ @!=@ work on any type, but both operands have to have the same type.
* Assignment to a function parameter is valid and should change the value as for a local variable
* The && and || operators are short-circuiting (this is already specified in the spec)
* If you have multiple return statements in one block then only the first is used, but the others should still be type checked.
* Indexing a scalar is a semantic error. @{ int x; x[0] = 1; }@ is a semantic error, and @{ int x; int y; y = x[0]; }@ is a semantic error, and the same if @x@ is a field variable.
* Indexing with a bool is a semantic error. @{ int xs[10]; int main() { int x; x = xs[true]; }@ is a semantic error.
* Using a non-bool expression for a loop condition is a semantic error. @{ while (1) {} }@ and @{ int x; for (x = 0; 1; x = x + 1) {} }@ are semantic errors.
* Using a non-bool expression in an if statement condition is a semantic error. @{ if (0) {} }@ is a semantic error.
* A return statement with an expression is not allowed in function with void return type. @{ void foo() { return (1); }@ and @{ void bar() {} void foo() { return (bar()); }@ are both semantic errors.
* A return statement with no expression in a non-void function produces an undefined return value. @{ int foo() { return; } int main() { int x; x = foo(); } }@ is a not a semantic error, but the value of @x@ may be anything (typicall zero due to zero initialization).
* Cannot use a void function in an expression. @void foo() {} int main() { if (foo()) {} }@ is invalid.
* Cannot call a method with the wrong number of arguments.

h3. Scoping Rules

This section clarifies the behaviour with scoping.

* Having two fields with the same name is a semantic error.
* Having two methods with the same name is a semantic error.
* Having a field and a method with the same name is a semantic error.
* externs count as methods for scoping.
* Having two local variables with the same name declared at the same block is a semantic error. @{ int x; int x; }@ is an error, but @{ int x; { int x; } }@ is ok.
* Having a local variable in the outer block of a method that has a parameter with the same name is a semantic error. @void foo(int x) { int x; }@ is an error, but @void foo(int x) { { int x; } }@ is ok.
* A function can be referred to anywhere in the program, including before its definition. @class C { void foo() { bar() }; void bar() {}; /* ... */ }@ is ok.
* Functions, fields, arguments, and local variables all share the same namespace (symbol table) and can shadow each other except for the above rules. e.g. in @class C { void foo() {}; void bar() { int foo; foo(); } /* ... */ }@ the "foo();" refers to the local int variable, not the function.
* @break@ and @continue@ only apply to the innermost containing loop. Using @break@ or @continue@ outside of a loop results in a semantic error.

h3. Statements

These are semantic errors that can occur when using statements in Decaf.

* There are no restrictions on the type of @main@.
* Assigning a scalar to an array is considered a type mismatch.
* The following produce undefined behaviour, but must not produce compile time semantic errors:
   - Using the value of any uninitialized scalar variable or array element
   - A function with no return statement is equivalent to ending the function with a return statement that has no expression: @return;@
   - Assigning to an array cell at an invalid index
* Any bool argument to a integer parameter must be converted while keeping its value, not just for @print_int@.
* Passing a argument to a function parameter with a different type is a semantic error except for the special case of passing a @bool@ as an @int@.
* Declaring an array of size less than or equal to zero is a semantic error.
* Assignment to a function parameter is valid and should change the value as for a local variable.


<notextile>
</div>
<div id="footer">
  <div class="container">
    <p class="text-muted credit">All content is &copy; <a href="http://www.cs.sfu.ca/~anoop/index.html">Anoop Sarkar</a>. The style sheet is from <a href="http://getbootstrap.com">Twitter Bootstrap</a>. The style and some details of this language reference were taken from the <a href="http://golang.org/ref/spec">Go Language Reference</a> although as yet Decaf and Go are very different from each other.</p>
  </div>
</div>
<!-- jQuery (necessary for Bootstrap's JavaScript plugins) -->
<script src="js/jquery.js"></script>
<!-- Include all compiled plugins (below), or include individual files as needed -->
<script src="js/bootstrap.min.js"></script>
</body>
</html>
</notextile>
